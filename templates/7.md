<!--
SPDX-License-Identifier: Apache-2.0
Copyright (c) 2025 nip
-->
# ROS 7 @ `device name`

## Basic
- [ ] Upgrade software
	- [ ] `System -> Resources`: View CPU architecture
		```
		/system/resource/print
		```
	- [ ] Download `Main package` @ Stable column 4 your architecture @ [link](https://mikrotik.com/download)
	- [ ] `Files -> Upload`: Upload main package with `.npk` extension 2 files @ MikroTik
	- [ ] `System -> Reboot -> OK`
		```
		/system/reboot
		```
- [ ] Upgrade firmware
	- [ ] `System -> RouterBOARD -> Upgrade`: Wait 4 red line about reboot 2 continue
		```
		/system/routerboard/upgrade
		```
	- [ ] `System -> Reboot -> OK`
		```
		/system/reboot
		```
- [ ] `System -> Reset Configuration -> No Default Configuration -> Reset Configuration`: Reset with no default config
	```
	/system/reset-configuration no-defaults=yes
	```
- [ ] @ localhost: Connect via Ethernet 2 MikroTik and setup static IP with only local routes 2 connect via WinBox by MAC-address in Neighbors table (if nothing - `Refresh` button)
- [ ] `System -> Users`
	- [ ] `New`: Add new non-default user with `full` rights and password
	- [ ] `Disconnect`: Logout from session @ WinBox
	- [ ] Login @ WinBox via new user & password
	- [ ] Select `admin` user & `Delete` it
- [ ] `Interfaces -> Interface`: Rename interfaces by opening each of them
	- [ ] 4 Ethernet ports 2 ISP - `eth\d\d*-wan`
		```
		/interface ethernet
		set [ find default-name=etherNUMBER ] name=ethNUMBER-wan
		```
	- [ ] 4 Ethernet ports 2 LAN - `eth\d\d*-lan`
		```
		/interface ethernet
		set [ find default-name=etherNUMBER ] name=ethNUMBER-lan
		```
- [ ] `Bridge -> Bridge -> New`: Add bridge `br` & @ `VLAN` tab enable `VLAN Filtering`
	```
	/interface bridge
	add name=br vlan-filtering=yes
	```
- [ ] `Interfaces -> VLAN -> New`: Add each of VLANs, setting up: `Name`, `VLAN ID`, `Interface` = `br`
	```
	/interface vlan
	add name=NAME vlan-id=VLAN_ID interface=br
	```
- [ ] `Bridge -> Ports -> New`: Add mapping of each port 2 bridge, setting up: `Interface`, `Bridge` = `br`, `PVID` @ `VLAN` tab (unttaged VLAN on this port)
	```
	/interface bridge port
	add interface=ethNUMBER-lan bridge=br pvid=VLAN_ID
	```
- [ ] `Bridge -> VLANs -> New`: Add mapping of interfaces 2 VLANs, adding rule per VLAN & setting up: `Bridge` = `br`, `VLAN IDs` as number of VLAN, `Tagged` as trunk interfaces with this VLAN, `Untagged` as access interfaces with this VLAN
	```
	/interface bridge vlan
	add bridge=br vlan-ids=VLAN_ID \
		tagged=ethNUMBER-lan,ethNUMBER-lan \
		untagged=ethNUMBER-lan,ethNUMBER-lan
	```
- [ ] Uplink connection
	- [ ] DHCP
		- [ ] `IP -> DHCP client -> New`: Add DHCP-client on each of uplink interfaces (Interface/VLAN)
			```
			/ip dhcp-client
			add interface=ethNUMBER-wan
			```
	- [ ] PPPoE
		- [ ] `PPP -> New -> PPPoE Client`: Add PPPoE-client on each of `eth\d\d*-wan` interfaces, setting up: `Name`, `Interfaces` as ISP Interface, `User` & `Password` @ `Dial Out` tab
			```
			/interface pppoe-client
			add name=NAME interface=ethNUMBER-wan user=USER password=PASSWORD
			```
- [ ] `System -> NTP client`: setup `Enabled` & `NTP Servers` as `[0-1].pool.ntp.org` (2 servers)
	```
	/system ntp client
	set enabled=yes
	/system ntp client servers
	add address=0.pool.ntp.org
	add address=1.pool.ntp.org
	```
- [ ] Kernel router
	- [ ] `IP -> Addresses -> New`: Add local static IP 2 this MikroTik, setting up: `Address` with mask like `X.X.X.1/Y`, `Interface` as Interface/VLAN 4 IP
		```
		/ip address
		add address=X.X.X.1/Y interface=VLAN_INTERFACE
		```
	- [ ] `IP -> DNS`: setup `Allow Remote Requests` & (optional) static DNS-servers @ `Servers`
		```
		/ip dns
		set allow-remote-requests=yes
		```
	- [ ] `System -> NTP server -> Enabled`: Enable NTP server
		```
		/system ntp server
		set enabled=yes
		```
	- [ ] `IP -> Pool -> New`: Add IP DHCP pool 4 each of VLANs (except static addresses pool)
		```
		/ip pool
		add name=NAME ranges=X.X.X.A-X.X.X.B
		```
	- [ ] `IP -> DHCP Server -> Networks -> New`: Add Network IP with mask like `X.X.X.0/Y` with setting up `Gateway`, DNS & NTP like `X.X.X.1` 4 each of VLANs
		```
		/ip dhcp-server network
		add address=X.X.X.0/Y \
			gateway=X.X.X.1 \
			dns-server=X.X.X.1 \
			ntp-server=X.X.X.1
		```
	- [ ] `IP -> DHCP Server -> DHCP -> New`: Add DHCP server 4 each of VLANs, setting up: `Name`, `Interface` as Interface/VLAN, `Lease Time`, `Address Pool`
		```
		/ip dhcp-server
		add name=NAME interface=NAME lease-time=LEASE_TIME address-pool=NAME
		```
- [ ] `Disconnect`: Logout from session @ WinBox
- [ ] @ localhost: Connect via Ethernet 2 mgmt MikroTik port and setup DHCP IP with only local routes 2 connect via WinBox by IP-address in Neighbors table (if nothing - `Refresh` button)
- [ ] Firewall: Add rules, don't forget 2 fix networks @ `Src. Address` & `Dst. Address` and bridges/interfaces/VLANs @ `In. Interface` & `Out. Interface` @ added rules:
	- [ ] `IP -> Firewall -> Filter Rules -> New`: Adding filter kind of rules with fields
		- [ ] Kernel router
			- [ ] (optional fasttrack for speed, not working with QoS) `Chain` = `Forward`, `Src. Address` as network IP with mask, `Dst. Address` as `!` network IP with mask, `In. Interface` as VLAN, `Out. Interface` as ISP Interface (From smth lan 2 smth wan) & `Action` @ `Action` tab = `fasttrack connection`
				```
				/ip firewall filter
				add chain=forward \
					src-address=NETWORK_IP_WITH_MASK \
					in-interface=VLAN_INTERFACE \
					dst-address=!NETWORK_IP_WITH_MASK \
					out-interface=ISP_INTERFACE \
					hw-offload=yes \
					action=fasttrack-connection
				```
			- [ ] `Chain` = `Forward`, `Src. Address` as network IP with mask, `Dst. Address` as `!` network IP with mask, `In. Interface` as VLAN, `Out. Interface` as ISP Interface (4m smth lan 2 smth wan) & `Action` @ `Action` tab = `accept`
				```
				/ip firewall filter
				add chain=forward \
					src-address=NETWORK_IP_WITH_MASK \
					in-interface=VLAN_INTERFACE \
					dst-address=!NETWORK_IP_WITH_MASK \
					out-interface=ISP_INTERFACE \
					action=accept
				```
			- [ ] (optional fasttrack for speed, not working with QoS) `Chain` = `Forward`, `Src. Address` as `!` network IP with mask, `Dst. Address` as network IP with mask, `In. Interface` as ISP Interface, `Out. Interface` as VLAN, `Connection State` = `Established, Related` (Answer 4m smth wan 2 smth lan) & `Action` @ `Action` tab = `fasttrack connection`
				```
				/ip firewall filter
				add chain=forward \
					src-address=!NETWORK_IP_WITH_MASK \
					in-interface=ISP_INTERFACE \
					dst-address=NETWORK_IP_WITH_MASK \
					out-interface=VLAN_INTERFACE \
					connection-state=established,related \
					hw-offload=yes \
					action=fasttrack-connection
				```
			- [ ] `Chain` = `Forward`, `Src. Address` as `!` network IP with mask, `Dst. Address` as network IP with mask, `In. Interface` as ISP Interface, `Out. Interface` as VLAN, `Connection State` = `Established, Related` (Answer 4m smth wan 2 smth lan) & `Action` @ `Action` tab = `accept`
				```
				/ip firewall filter
				add chain=forward \
					src-address=!NETWORK_IP_WITH_MASK \
					in-interface=ISP_INTERFACE \
					dst-address=NETWORK_IP_WITH_MASK \
					out-interface=VLAN_INTERFACE \
					connection-state=established,related \
					action=accept
				```
			- [ ] `Chain` = `Input`, `Src. Address` as mgmt network IP with mask, `Protocol` = `udp`, `Dst. Port` = `5246,5247`, `In. Interface` as mgmt VLAN (CAPsMAN) & `Action` @ `Action` tab = `accept`
				```
				/ip firewall filter
				add chain=input \
					protocol=udp dst-port=5246,5247 \
					in-interface=MGMT_VLAN_INTERFACE \
					src-address=MGMT_NETWORK_IP_WITH_MASK \
					action=accept
				```
			- [ ] `Chain` = `Input`, `Src. Address` as network IP with mask, `Protocol` = `udp`, `Dst. Port` = `53,123`, `In. Interface` as VLAN (DNS, NTP) & `Action` @ `Action` tab = `accept`
				```
				/ip firewall filter
				add chain=input \
					protocol=udp dst-port=53,123 \
					in-interface=VLAN_INTERFACE \
					src-address=NETWORK_IP_WITH_MASK \
					action=accept
				```
		- [ ] `Chain` = `Input`, `Connection State` = `Established, Related` (Reply 2 this MikroTik) & `Action` @ `Action` tab = `accept`
			```
			/ip firewall filter
			add chain=input \
				connection-state=established,related \
				action=accept
			```
		- [ ] `Chain` = `Input`, `In. Interface` = `lo` (Loopback) & `Action` @ `Action` tab = `accept`
			```
			/ip firewall filter
			add chain=input \
				in-interface=lo \
				action=accept
			```
		- [ ] `Chain` = `Input`, `Src. Address` as network IP with mask, `Protocol` = `icmp`, `In. Interface` as VLAN (Ping) & `Action` @ `Action` tab = `accept`
			```
			/ip firewall filter
			add chain=input \
				protocol=icmp \
				in-interface=VLAN_INTERFACE \
				src-address=NETWORK_IP_WITH_MASK \
				action=accept
			```
		- [ ] `Chain` = `Input`, `Src. Address` as mgmt network IP with mask, `Protocol` = `tcp`, `Dst. Port` = `22,80,8291`, `In. Interface` as mgmt VLAN (Remote configuration) & `Action` @ `Action` tab = `accept`
			```
			/ip firewall filter
			add chain=input \
				protocol=tcp dst-port=22,80,8291 \
				in-interface=MGMT_VLAN_INTERFACE \
				src-address=MGMT_NETWORK_IP_WITH_MASK \
				action=accept
			```
		- [ ] `Chain` = `Output`,`Connection State` = `! Invalid` (Egress from this MikroTik) & `Action` @ `Action` tab = `accept`
			```
			/ip firewall filter
			add chain=output \
				connection-state=!invalid \
 				action=accept
			```
		- [ ] Drops
			- [ ] `Chain` = `Input` & `Action` @ `Action` tab = `drop`
				```
				/ip firewall filter
				add chain=input \
 					action=drop
				```
			- [ ] `Chain` = `Output` & `Action` @ `Action` tab = `drop`
				```
				/ip firewall filter
				add chain=output \
 					action=drop
				```
			- [ ] `Chain` = `Forward` & `Action` @ `Action` tab = `drop`
				```
				/ip firewall filter
				add chain=forward \
 					action=drop
				```
	- [ ] `IP -> Firewall -> NAT -> New`: Adding NAT kind of rules with fields
		- [ ] `Chain` = `srcnat`, `Src. Address` as network IP with mask, `Dst. Address` as `!` network IP with mask, `Out. Interface` as ISP Interface (Masquerade smth lan 2 smth wan) & `Action` @ `Action` tab = `masquerade`
			```
			/ip firewall filter
			add chain=srcnat \
				src-address=NETWORK_IP_WITH_MASK \
				dst-address=!NETWORK_IP_WITH_MASK \
				out-interface=ISP_INTERFACE \
				action=masquerade
			```
- [ ] `System -> Identity`: Setup name 4 MikroTik
	```
	/system identity
	set name=NAME
	```
- [ ] WiFi
	- [ ] Kernel router
		- [ ] `WiFi -> Security -> New`: Add security profiles, setting up: `Name`, `Authentication Types` = `WPA2 PSK`, `Encryption` = `CCMP`, `Disable PMKID` = `true`, `WPS` = `disable`
			```
			/interface wifi security
			add name=NAME authentication-types=wpa2-psk disabled=no encryption=ccmp \
				passphrase=PASSWORD wps=disable disable-pmkid=yes
			```
		- [ ] `WiFi -> Datapath -> New`: Add datapath profiles, setting up: `Name`, `Bridge` = `br`, `VLAN ID` as int of VLAN
			```
			/interface wifi datapath
			add bridge=br disabled=no name=NAME vlan-id=VLAN_ID
			```
		- [ ] `WiFi -> Channel -> New`: Add channel profiles, setting up:
			- [ ] `Name` = `2_4`, `Band` as maximum supported 2.4GHz (`2GHz AX` or `2GHz N`), `Channel Width` = `20MHz`, `Frequency`: `2412`, `2437`, `2462`
				```
				/interface wifi channel
				add name=2_4 band=BAND disabled=no width=20mhz frequency=2437,2462,2412
				```
			- [ ] `Name` = `5`, `Band` as maximum supported 5GHz (`5GHz AX` or `5GHz AC`), `Channel Width` = `20/40/80MHz`, `Frequency`: `5180`, `5260`, `5660`
				```
				/interface wifi channel
				add name=5 band=BAND disabled=no width=20/40/80mhz \
					frequency=5180,5260,5660
				```
		- [ ] `WiFi -> Configuration -> New`: Add channel profiles, setting up: `Name`, `Mode` = `ap`, `SSID`, `Country`, `Channel` only if it's master configuration, `Security` @ `Security` tab, `Datapath` @ `Datapath` tab
			```
			/interface wifi configuration
			add name=NAME \
				ssid=SSID \
				channel=CHANNEL_PROFILE \
				country=COUNTRY \
				datapath=DATAPATH_PROFILE \
				security=SECURITY_PROFILE \
				disabled=no mode=ap 
			```
		- [ ] `WiFi -> Provisioning -> New`: Add provisioning profiles for each of bands, setting up: `Supported Bands`, `Action` = `create dynamic enabled`, `Master Configuration` as main WiFi, `Slave Configurations` as additional WiFis & note that `Identity Regexp` not true regexp, only `*smth*`
			```
			/interface wifi provisioning
			add supported-bands=BAND action=create-dynamic-enabled disabled=no master-configuration=CONFIGURATION_PROFILE
			```
		- [ ] `WiFi -> Remote CAP -> CAPsMAN`: Setup `Enabled`, `Interfaces` as mgmt VLAN, `CA Certificate` & `Certificate` as `auto`
			```
			/interface wifi capsman
			set enabled=yes interfaces=MGMT_VLAN_INTERFACE ca-certificate=auto certificate=auto \
				package-path="" require-peer-certificate=no upgrade-policy=none
			```
	- [ ] AP - look @ [link](https://help.mikrotik.com/docs/spaces/ROS/pages/1409138/Wireless) for additional packages (802.11ac or 802.11ax devices) @ [link](https://mikrotik.com/download)
		- [ ] `WiFi -> WiFi -> CAP`: Setup `Enabled`, `Discovery Interfaces` as mgmt VLAN, (optional) `CAPsMAN Addresses` as mgmt VLAN IP of Kernel router
			```
			/interface wifi cap
			set discovery-interfaces=MGMT_VLAN_INTERFACE enabled=yes
			```
- [ ] Wireless
	- [ ] Kernel router - look @ [link](https://help.mikrotik.com/docs/spaces/ROS/pages/1409138/Wireless) for additional packages (`wireless` package leads to loses built-in newer wlan cards) @ [link](https://mikrotik.com/download)
		- [ ] `Wireless -> CAPsMAN -> Security Cfg. -> New`: Add security profiles, setting up: `Name`, `Authentication Type` = `WPA2 PSK`, `Encryption` = `aes ccm`, `Disable PMKID` = `true`
			```
			/caps-man security
			add name=NAME authentication-types=wpa2-psk disable-pmkid=yes encryption=aes-ccm \
				passphrase=PASSWORD
			```
		- [ ] `Wireless -> CAPsMAN -> Datapath -> New`: Add datapath profiles, setting up: `Name`, `Bridge` = `br`, `VLAN Mode` = `use tag`, `VLAN ID` as int of VLAN
			```
			/caps-man datapath
			add name=NAME bridge=br vlan-id=VLAN_ID vlan-mode=use-tag
			```
		- [ ] `Wireless -> CAPsMAN -> Channel -> New`: Add channel profiles, setting up: `Name`, (2.4: 2412, 2437, 2462 (20mhz, extension channel: disabled)) (5: 5180, 5260, 5660) <!-- Refactor -->
			- [ ] `Name` = `2_4`, `Frequency`: `2412`, `2437`, `2462`, `Control Channel Width` = `20MHz`, `Band` as maximum supported 2.4GHz (`2ghz-g/n`), `Extension Channel` = `disabled`
				```
				/caps-man channel
				add name=2_4 frequency=2412,2437,2462 control-channel-width=20mhz \
					band=2ghz-g/n extension-channel=disabled
				```
			- [ ] `Name` = `5`, `Frequency`: `5180`, `5260`, `5660`, `Control Channel Width`= `40MHz turbo`, `Band` as maximum supported 5GHz (`5ghz-n/ac`)
				```
				/caps-man channel
				add name=5 frequency=5180,5260,5660 control-channel-width=40mhz-turbo \
					band=5ghz-n/ac extension-channel=disabled
				```
		- [ ] `Wireless -> CAPsMAN -> Configuration -> New`: Add configuration profiles, setting up: `Name`, `Mode` = `ap`, `SSID`, `Country`, `Channel` @ `Channel` tab only if it's master configuration, `Datapath` @ `Datapath` tab, `Security` @ `Security` tab
			```
			/caps-man configuration
			add name=NAME \
				ssid=SSID \
				channel=CHANNEL_PROFILE \
				country=COUNTRY \
				datapath=DATAPATH_PROFILE \
				security=SECURITY_PROFILE \
				mode=ap
			```
		- [ ] `Wireless -> CAPsMAN -> Provisioning -> New`: Add provisioning profiles for each of bands, setting up: `Hw. Supported Modes` = `g/n` or `ac`, `Action` = `create dynamic enabled`, `Master Configuration` as main WiFi, `Slave Configurations` as additional WiFis & note that `Identity Regexp` not true regexp, only `*smth*`
			```
			/caps-man provisioning
			add hw-supported-modes=gn action=create-dynamic-enabled master-configuration=CONFIGURATION_PROFILE
			```
		- [ ] `Wireless -> CAPsMAN -> CAP Interface -> Manager`: Setup `Enabled`, `Interfaces`: `-> New` - add mgmt VLAN with `Forbid` = `false`, select `all` & setup `Forbid` = `true`
			```
			/caps-man manager
			set enabled=yes
			/caps-man manager interface
			set [ find default=yes ] forbid=yes
			add disabled=no interface=MGMT_VLAN_INTERFACE
			```
	- [ ] AP - look @ [link](https://help.mikrotik.com/docs/spaces/ROS/pages/1409138/Wireless) for additional packages @ [link](https://mikrotik.com/download)
		- [ ] `Wireless -> Wireless -> WiFi Interfaces -> CAP`: Setup `Enabled`, `Interfaces` - wlan interfaces, `Discovery Interfaces` as mgmt VLAN, (optional) `CAPsMAN Addresses` as mgmt VLAN IP of Kernel router, `Bridge` = `br`
			```
			/interface wireless cap
			set bridge=br discovery-interfaces=MGMT_VLAN_INTERFACE enabled=yes interfaces=wlan1
			```
- [ ] `IP -> Services`: Disable services: `api`, `api-ssl`, `ftp`, `telnet`
	```
	/ip service
	set ftp disabled=yes
	set telnet disabled=yes
	set api disabled=yes
	set api-ssl disabled=yes
	```
- [ ] `IP -> Neighbors -> Discovery Settings`: Disble neighbors, setting up: `Interface` = `none`, `Protocol` - disable all of them
	```
	/ip neighbor discovery-settings
	set discover-interface-list=none protocol=""
	```
- [ ] Disable MAC servers
	- [ ] `Tools -> MAC Server -> MAC Telnet Server`: Setup `Allowed Interface List` = `none`
		```
		/tool mac-server
		set allowed-interface-list=none
		```
	- [ ] `Tools -> MAC Server -> MAC WinBox Server`: Setup `Allowed Interface List` = `none`
		```
		/tool mac-server mac-winbox
		set allowed-interface-list=none
		```
	- [ ] `Tools -> MAC Server -> MAC Ping Server`: Setup `MAC Ping Server Enabled` = `false`
		```
		/tool mac-server ping
		set enabled=no
		```
- [ ] `IP -> UPnP`: Setup `Enabled` (external & internal interfaces)
	```
	/ip upnp
	set enabled=yes
	```
	- [ ] `IP -> UPnP -> Interfaces -> New`: Add interfaces
		- [ ] ISP ports: `Interface` as ISP port, `Type`: `external`
			```
			/ip upnp interfaces
			add interface=ISP_INTERFACE type=external
			```
		- [ ] VLAN: `Interface` as VLAN, `Type`: `internal`
			```
			/ip upnp interfaces
			add interface=VLAN_INTERFACE type=internal
			```
- [ ] `Files -> Backup`: Make proprietary backup, setting up: `Name`, `Password`
- [ ] @ localhost: Make raw backup: `ssh user@IP export show-sensitive > backup.rsc`
- [ ] Download backups
- [ ] Backup backups
